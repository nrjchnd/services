#!/usr/bin/perl

use strict;
use English qw/-no_match_vars/;
use CGI::Simple;
use URI::Escape;
use HTML::Entities;
use LWP::UserAgent;
use HTTP::Cookies;
use XML::LibXML;

# directory containing the SEPMAC.cnf.xml files
our $TFTPBOOT_DIRECTORY = '/var/lib/tftpboot';

# login credentials set in manager.conf
our $MANAGER_HOSTNAME = 'localhost:8088';
our $MANAGER_USERNAME = 'asterisk';
our $MANAGER_PASSWORD = 'asterisk';

sub how_to_use {
    my $cgi = shift;

    print '<CiscoIPPhoneText>',
            '<Title>How To Use</Title>',
            '<Text>Use the keypad or navigation key to select the first letter of the person\'s name.</Text>',
            '<Prompt>Your current options</Prompt>',
            '<SoftKeyItem>',
              '<Name>Back</Name>',
              '<URL>SoftKey:Exit</URL>',
              '<Position>', $cgi->http ('X-Cisco-IPPhoneModelName') =~ m/^CP-79/ ? 3 : 1, '</Position>',
            '</SoftKeyItem>',
          '</CiscoIPPhoneText>';
}

sub directory_index {
    my $cgi = shift;

    print '<CiscoIPPhoneMenu>',
            '<Title>Local Directory</Title>';

   foreach my $index ('1', '2 ABC', '3 DEF', '4 GHI', '5 JKL', '6 MNO', '7 PRQS', '8 TUV', '9 WXYZ', '*', '0', '#') {
       print '<MenuItem>',
               '<Name>', encode_entities ($index), '</Name>',
               '<URL>', $cgi->url, '?name=', encode_entities ($cgi->param ('name')),
                 '&amp;menuitem=yes&amp;index=', encode_entities ($index), '</URL>',
             '</MenuItem>';
   }

   print   '<SoftKeyItem>',
             '<Name>Exit</Name>',
             '<URL>Init:Directories</URL>',
             '<Position>', $cgi->http ('X-Cisco-IPPhoneModelName') =~ m/^CP-79/ ? 3 : 1, '</Position>',
           '</SoftKeyItem>',
           '<SoftKeyItem>',
             '<Name>Back</Name>',
             '<URL>SoftKey:Select</URL>',
             '<Position>', $cgi->http ('X-Cisco-IPPhoneModelName') =~ m/^CP-79/ ? 1 : 2, '</Position>',
           '</SoftKeyItem>',
           '<SoftKeyItem>',
             '<Name>Help</Name>',
             '<URL>', $cgi->url, '?name=', encode_entities ($cgi->param ('name')),
               '&amp;menuitem=yes&amp;index=howtouse</URL>',
             '<Position>', $cgi->http ('X-Cisco-IPPhoneModelName') =~ m/^CP-79/ ? 2 : 3, '</Position>',
           '</SoftKeyItem>',
         '</CiscoIPPhoneMenu>';
}

sub directory_entries {
    my $cgi = shift;

    my $useragent = LWP::UserAgent->new;

    $useragent->cookie_jar (HTTP::Cookies->new (hide_cookie2 => 1));

    my ($request, $response);

    $request  = HTTP::Request->new (GET => 'http://' . $MANAGER_HOSTNAME . '/mxml?Action=Login'
                                    . '&Username=' . uri_escape ($MANAGER_USERNAME)
                                    . '&Secret=' . uri_escape ($MANAGER_PASSWORD));
    $response = $useragent->request ($request);

    die $response->as_string unless ($response->is_success);

    $request  = HTTP::Request->new (GET => 'http://' . $MANAGER_HOSTNAME . '/mxml?Action=SIPPeers');
    $response = $useragent->request ($request);

    die $response->as_string unless ($response->is_success);

    my $document = XML::LibXML->load_xml (string => $response->content);

    my $directory = {};

    foreach my $element ($document->findnodes ('/ajax-response/response/generic[@event="PeerEntry"]')) {
        my $peer = $element->getAttribute ('objectname');

        # this will be slow if you have a large number of SIP peers
        $request  = HTTP::Request->new (GET => 'http://' . $MANAGER_HOSTNAME . '/mxml?Action=SIPShowPeer'
                                        . '&Peer=' . uri_escape ($peer));
        $response = $useragent->request ($request);

        die $response->as_string unless ($response->is_success);

        my $document = XML::LibXML->load_xml (string => $response->content);

        my ($name, $phone_number)
            = ($document->findvalue ('/ajax-response/response/generic/@callerid') =~ m/^"([^\"]*)" <([^>]*)>$/);

        next unless ($phone_number =~ m/^\d+$/);
        next if (index ($cgi->param ('index'), ucfirst substr ($name, 0, 1)) == -1);

        $directory->{$phone_number} = $name;
    }

    $request  = HTTP::Request->new (GET => 'http://' . $MANAGER_HOSTNAME . '/mxml?Action=Logoff');
    $response = $useragent->request ($request);

    die $response->as_string unless ($response->is_success);

    print '<CiscoIPPhoneMenu>',
            '<Title>', encode_entities ($cgi->param ('index')), '</Title>';

    foreach my $phone_number (sort ({$directory->{$a} cmp $directory->{$b}} keys %{$directory})) {
       my $name = $directory->{$phone_number};

       print '<MenuItem>',
               '<Name>', encode_entities ($name), '</Name>',
               '<URL>Dial:', encode_entities ($phone_number), '</URL>',
             '</MenuItem>';
    }

    print  '<SoftKeyItem>',
             '<Name>Exit</Name>',
             '<URL>', $cgi->url, '?name=', encode_entities ($cgi->param ('name')),
               '&amp;menuitem=yes</URL>',
             '<Position>', $cgi->http ('X-Cisco-IPPhoneModelName') =~ m/^CP-79/ ? 3 : 1, '</Position>',
           '</SoftKeyItem>',
           '<SoftKeyItem>',
             '<Name>Dial</Name>',
             '<URL>SoftKey:Select</URL>',
             '<Position>', $cgi->http ('X-Cisco-IPPhoneModelName') =~ m/^CP-79/ ? 1 : 2, '</Position>',
           '</SoftKeyItem>',
         '</CiscoIPPhoneMenu>';
}

eval {
    my $cgi = CGI::Simple->new;

    # check that the device exists
    unless ($cgi->param ('name') =~ m/^SEP[0-9A-F]{12}$/
            && stat ($TFTPBOOT_DIRECTORY . '/' . $cgi->param ('name') . '.cnf.xml')) {
        print 'Status: 403 Forbidden', "\r\n",
              'Content-Type: text/plain', "\r\n",
              "\r\n",
              'Invalid or unknown device name';

        return;
    }

    print 'Status: 200 OK', "\r\n",
          'Content-Type: text/xml', "\r\n",
          "\r\n";

    # 7900 series need a menu item displayed first
    if ($cgi->http ('X-Cisco-IPPhoneModelName') =~ m/^CP-79/ && !length $cgi->param ('menuitem')) {
        print '<CiscoIPPhoneMenu>',
                '<MenuItem>',
                  '<Name>Local Directory</Name>',
                  '<URL>', $cgi->url, '?name=', encode_entities ($cgi->param ('name')),
                    '&amp;menuitem=yes</URL>',
                '</MenuItem>',
              '</CiscoIPPhoneMenu>';
    } elsif ($cgi->param ('index') eq 'howtouse') {
        how_to_use ($cgi);
    } elsif ($cgi->param ('index') =~ m/^[A-Z0-9*#]+$/) {
        directory_entries ($cgi);
    } else {
        directory_index ($cgi);
    }
};

warn $EVAL_ERROR if (length $EVAL_ERROR);
