#!/usr/bin/perl

use strict;
use English qw/-no_match_vars/;
use CGI::Simple;
use POSIX qw/strftime/;

# directory containing the SEPMAC.cnf.xml files
our $TFTPBOOT_DIRECTORY = '/var/lib/tftpboot';

# directory to store uploaded log files
our $PRT_LOGS_DIRECTORY = '/var/log/cisco';

eval {
    $CGI::Simple::DISABLE_UPLOADS = 0;
    $CGI::Simple::POST_MAX = -1;

    my $cgi = CGI::Simple->new;

    # check that the device exists
    unless ($cgi->param ('devicename') =~ m/^SEP[0-9A-F]{12}$/
            && stat ($TFTPBOOT_DIRECTORY . '/' . $cgi->param ('devicename') . '.cnf.xml')) {
        print 'Status: 403 Forbidden', "\r\n",
              'Content-Type: text/plain', "\r\n",
              "\r\n",
              'Invalid or unknown device name';

        return;
    }

    my $time = strftime ('%Y%m%d%H%M', localtime time);

    unless ($cgi->upload ($cgi->param ('prt_file'), $PRT_LOGS_DIRECTORY . '/' . $cgi->param ('devicename') . '-' . $time . '.tar.gz')) {
        print 'Status: 500 Internal Server Error', "\r\n",
              'Content-Type: text/plain', "\r\n",
              "\r\n",
              'Unable to create log file';

        return;
    }

    print 'Status: 200 OK', "\r\n",
          'Content-Type: text/plain', "\r\n",
          "\r\n",
          'Log file saved';
};

warn $EVAL_ERROR if (length $EVAL_ERROR);
